<html>
  <head>
    <title>Diffusion Model</title>
    <script src="../lib/agentbase.js"></script>
    <script src="../tools/coffee-script.js"></script>
    <script type="text/coffeescript">
    # AgentBase is Free Software, available under GPL v3 or any later version.
    # Original AgentScript code @ 2013, 2014 Owen Densmore and RedfishGroup LLC.
    # AgentBase (c) 2014, Wybo Wiersma.

    # Diffusion has randomly flying agents on a patch grid dropping a color
    # which is diffused over the grid.

    u = ABM.util

    class ABM.DiffusionModel extends ABM.Model
      setup: ->
        # No optimizations: 30fps
        @patches.usePixels() # 57+fps

        # globals
        @population = 75
        @diffusionRate = .70
        @coolingRate = .10
        @speed = .5
        
        @animator.setRate 30, true
        
        # defaults
        @agents.setDefault "size", 1.5

        for p in @patches.create()
          p.heat = 0

        @agents.create @population
        # @agents.create @population, (a) => # fat arrow for @patches etc
        #   a.size = 1.5

      step: ->
        console.log @animator.toString() if @animator.ticks % 100 is 0
        for patch in @patches
          patch.heat *= (1 - @coolingRate)

        for agent in @agents
          agent.rotate u.randomCentered(u.degreesToRadians(40))
          agent.forward @speed
          agent.patch.heat = 1

        @patches.diffuse "heat", @diffusionRate, u.color.red
        # @stop() # debug: one step

    model = new ABM.DiffusionModel {
      div: "world",
      patchSize: 10,
      min: {x: -25, y: -20},
      max: {x: 25, y: 20}
      isTorus: true
    }
    model.start()
    </script>
  </head>
  <body>
    <div id="world"></div>
  </body>
</html>
