<html>
  <head>
    <title>Buttons Model</title>
    <script src="../lib/agentbase.js"></script>
    <script src="../tools/coffee-script.js"></script>
    <script type="text/coffeescript">
    # AgentBase is Free Software, available under GPL v3 or any later version.
    # Original AgentScript code @ 2013, 2014 Owen Densmore and RedfishGroup LLC.
    # AgentBase (c) 2014, Wybo Wiersma.

    # Buttons provides Stuart Kauffman's example of randomly connecting pairs
    # of buttons in a pile resulting in a tipping point.

    u = ABM.util

    class ABM.ButtonsModel extends ABM.Model
      setup: ->
        @refreshPatches = false # for static patches
        @refreshLinks = false # for static links .. drown as created
        
        # globals
        @buttons = 100
        @cluster = 0
        
        # defaults
        @agents.setDefault "shape", "circle"
        @agents.setDefault "size", 1.5
        @agents.setDefault "heading", 0 # override promotion to random angle
        @links.setDefault "thickness", .5 # 2.5
        
        @patches.create()
        @agents.create @buttons, (a) => # fat arrow for @patches etc
          # a.size = 1.5
          # a.shape = "circle"
          a.moveTo @patches.randomPoint()

      step: ->
        b1 = @agents.sample()
        b2 = @agents.other(b1).sample()
        
        @links.create b1, b2, (l) =>
          l.draw @contexts.links
        
        graph = @graphOf b1
        color = graph.min("id").color

        for node in graph
          node.color = color

        if graph.length > @cluster
          console.log "New largest cluster: size: #{graph.length}" +
            " tick: #{@animator.ticks}"

        @cluster = Math.max @cluster, graph.length
        
        if @cluster is @buttons
          console.log "Completed at tick #{@animator.ticks}:" +
            " Cluster size #{@cluster} Links: #{@links.length}"
          @stop()
        
      graphOf: (node) ->
        array = new ABM.Array

        for agent in @agents
          agent.marked = false

        @markNeighbors node

        for agent in @agents
          if agent.marked
            array.push agent

        array

      markNeighbors: (node) ->
        node.marked = true
        unMarked = (n for n in node.linkNeighbors() when not n.marked)
        @markNeighbors n for n in unMarked

    model = new ABM.ButtonsModel {
      div: "world"
    }
    model.start()

    </script>
  </head>
  <body>
    <div id="world"></div>
  </body>
</html>
