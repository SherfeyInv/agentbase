<html>
  <head>
    <title>Simple Model</title>
    <script src="../lib/agentbase.js"></script>
    <script src="../tools/coffee-script.js"></script>
    <script type="text/coffeescript">
    # This is a simple template for creating models.
    #
    # To build your own model, extend class ABM.Model supplying the 
    # two built in methods `setup` and `step`.
    #
    #`@foo` signifies a variable or method that is part of the Model
    # class.
    #
    # Note how this example uses CoffeeScript directly within the
    # browser via [text/coffeescript tags](http://coffeescript.org/#scripts):
    
    u = ABM.util # shortcut for ABM.util, some tools

    log = (arg) -> console.log arg # log to the browser console

    class MyModel extends ABM.Model
      # Initialize our model via the `setup` method. This model simply
      # creates a population of agents with arbitrary shapes on a grid
      # of patches colored in random shades of gray.
      #
      setup: -> # called by Model constructor
        # When agents move, they will wiggle. This sets the 
        # degrees/radians to wiggle, which is used further below.
        #
        @wiggle = u.degreesToRadians(30)

        for patch in @patches.create()
          patch.color = u.randomGray()

        for agent in @agents.create 25
          agent.size = 3
          agent.shape = u.shapes.names().sample()

      # Update, or run our model for one step, via the second built in
      # method, `step`.
      #
      # The agents wiggle, and move forward.
      #
      step: -> # called by Model.animate
        for agent in @agents
          agent.rotate u.randomCentered @wiggle
          agent.forward 1


    # Now that we've build our class, we call it with Model's
    # constructor arguments:
    #
    #   div: name of the div
    #   isTorus: map curls around the edges = has torus topology.
    #
    model = new MyModel({
      div: "layers",
      isTorus: true
    })
    model.start()
    </script>
  </head>
  <body>
    <div id="layers"></div>
  </body>
</html>
