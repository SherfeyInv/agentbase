<html>
  <head>
    <title>Ants Model</title>
    <script src="../lib/agentscript.js"></script>
    <script src="../tools/coffee-script.js"></script>
    <script type="text/coffeescript">

    u = ABM.util # ABM.util alias, u.s is also ABM.shape accessor.
    class MyModel extends ABM.Model
      setup: ->
        
        # No optimizations: 10fps
        @patches.usePixels() # 24fps
        # two: 27fps, now static colors larger percent of faster patches.
        @agents.setUseSprites() # 24->46-48fps!
        
        # @animator.setRate 25, true # normal: let agents step faster than drawing
        @animator.setRate 60, false # speed test
        
        # globals
        @nestColor = [255,255,0]  # yellow
        @foodColor = [0,0,255]    # blue

        # defaults
        @agents.setDefault "shape", "bug"
        @agents.setDefault "color", @foodColor # so default sprite not random color
        @agents.setDefault "size", 3
        
        # UI globals:
        @population = 255
        @maxPheromone = 35
        @diffusionRate = .30
        @evaporationRate = .01
        @wiggleAngle = u.degreesToRadians(30) # radians

        if @agents.useSprites
          # @nestSprite = ABM.shapes.shapeToSprite "bug", @nestColor, 3*@patches.size
          # @foodSprite = ABM.shapes.shapeToSprite "bug", @foodColor, 3*@patches.size
          @nestSprite = ABM.shapes.shapeToSprite "bug", @nestColor, @patches.toBits 3
          @foodSprite = ABM.shapes.shapeToSprite "bug", @foodColor, @patches.toBits 3
        
        @setupPatches()
        @setupAgents()
      step: ->
        console.log @animator.toString() if @animator.ticks % 100 is 0
        @updateAgents()
        @updatePatches()

      setupPatches: ->
        for patch in @patches.create()
          patch.isNest = false
          patch.isFood = false
          patch.foodPheromone = 0
          patch.nestPheromone = 0
        for patch in @patchRectangle @patches.maxX - 6, 0, 3, 3
          patch.isNest = true
          patch.color = @nestColor
        for patch in @patchRectangle @patches.minX + 6, 0, 3, 3
          patch.isFood = true
          patch.color = @foodColor
        null
      setupAgents: -> # a is agent
        @agents.create @population, (a) => # fat arrow for @patches etc
          #a.shape = "bug"
          #a.size = 3
          a.setXY @patches.maxX-6, 0
          @resetAgent(a, false) # sets a.pheromone to max
      resetAgent: (a, withFood) ->
        a.carryingFood = withFood
        if @agents.useSprites
          a.sprite = if withFood then @nestSprite else @foodSprite
          # a.setSprite (if withFood then @nestSprite else @foodSprite)
        else
          a.color = if withFood then @nestColor else @foodColor
          # a.colorString = u.colorString a.color if @agents.staticColors
        a.pheromone = @maxPheromone

      updateAgents: ->
        for a in @agents when a.id < @animator.ticks # gradually leave nest
            @wiggleUphill a
            @dropPheromone a
        null # needed to avoid returning an absurd comprehension
      wiggleUphill: (a) ->
        if a.patch.isOnEdge()
          # a.rotate Math.PI 
          a.rotate u.degreesToRadians(180)
        else
          nAhead = a.patch.neighbors(heading: a.heading, cone: u.degreesToRadians(180), radius: 2)
          [p, max] = nAhead.max ((n) => @targetPheromone(a, n)), true # => for @
          a.face p if max > .001/@maxPheromone
          # p = nAhead.max ((n) => @targetPheromone(a, n)) # => for @
          # max = @targetPheromone(a, p)
          # a.face p if max > .001/@maxPheromone
        a.rotate u.randomCentered(@wiggleAngle)
        a.forward 1
      targetPheromone: (a, patch) ->
        if a.carryingFood then patch.nestPheromone else patch.foodPheromone
      dropPheromone: (a) ->
        if (not a.carryingFood and a.patch.isFood) or (a.carryingFood and a.patch.isNest)
          @resetAgent a, not a.carryingFood
        if a.carryingFood
        then a.patch.foodPheromone += 0.1 * a.pheromone
        else a.patch.nestPheromone += 0.1 * a.pheromone
        a.pheromone *= 0.9
      
      updatePatches: ->
        @patches.diffuse "nestPheromone", @diffusionRate
        @patches.diffuse "foodPheromone", @diffusionRate
        for patch in @patches
          patch.foodPheromone *= (1 - @evaporationRate)
          patch.nestPheromone *= (1 - @evaporationRate)
          patch.foodPheromone = patch.nestPheromone = 0 if patch.isOnEdge()
          unless (patch.isNest or patch.isFood) #  or patch.isOnEdge()
            if patch.foodPheromone > patch.nestPheromone
              patch.fractionOfColor @foodColor, patch.foodPheromone
            else
              patch.fractionOfColor @nestColor, patch.nestPheromone
        null

      # utilities
      patchRectangle: (x, y, dw, dh) ->
        @patches.patchRectangle @patches.patch(x,y), dw, dh, true

    # div, patchSize, minX, maxX, minY, maxY, isTorus
    #   Defaults: 13, -16, 16, -16, 16, false
    model = new MyModel {
      div: "layers",
      size: 6,
      minX: -40,
      maxX: 40,
      minY: -40,
      maxY: 40
    }
    model.debug() # Debug: Put Model vars in global name space
    model.start() # Run model immediately after startup initialization
    </script>

  </head>
  <body>
    <div id="layers"></div>
  </body>
</html>
