<html>
  <head>
    <title>Ants Model</title>
    <script src="../lib/agentbase.js"></script>
    <script src="../tools/coffee-script.js"></script>
    <script type="text/coffeescript">
    # AgentBase is Free Software, available under GPL v3 or any later version.
    # Original AgentScript code @ 2013, 2014 Owen Densmore and RedfishGroup LLC.
    # AgentBase (c) 2014, Wybo Wiersma.

    u = ABM.util

    class ABM.AntsModel extends ABM.Model
      setup: ->
        # No optimizations: 10fps
        @patches.usePixels() # 24fps
        # two: 27fps, now static colors larger percent of faster patches.
        @agents.setUseSprites() # 24->46-48fps!
        
        @animator.setRate 25, false
        
        # globals
        @nestColor = [255, 255, 0]  # yellow
        @foodColor = [0, 0, 255]    # blue

        # defaults
        @agents.setDefault "shape", "bug"
        # so default sprite not random color
        @agents.setDefault "color", @foodColor
        @agents.setDefault "size", 3
        
        # UI globals:
        @population = 155
        @maxPheromone = 35
        @diffusionRate = .30
        @evaporationRate = .01
        @wiggleAngle = u.degreesToRadians(30) # radians

        if @agents.useSprites
          @nestSprite = u.shapes.shapeToSprite "bug", @nestColor,
              @patches.toBits 18
          @foodSprite = u.shapes.shapeToSprite "bug", @foodColor,
              @patches.toBits 18
        
        @setupPatches()
        @setupAgents()

      step: ->
        if @animator.ticks % 100 is 0
          console.log @animator.toString()

        @updateAgents()
        @updatePatches()

      setupPatches: ->
        for patch in @patches.create()
          patch.isNest = false
          patch.isFood = false
          patch.foodPheromone = 0
          patch.nestPheromone = 0

        for patch in @patchRectangle {x: @patches.max.x - 6, y: 0}, 3, 3
          patch.isNest = true
          patch.color = @nestColor

        for patch in @patchRectangle {x: @patches.min.x + 6, y: 0}, 3, 3
          patch.isFood = true
          patch.color = @foodColor

        null

      setupAgents: ->
        @agents.create @population, (agent) => # fat arrow for @patches etc
          agent.moveTo x: @patches.max.x - 6, y: 0
          @resetAgent(agent, false) # sets a.pheromone to max

      resetAgent: (agent, withFood) ->
        agent.carryingFood = withFood
        if @agents.useSprites
          if withFood
            agent.sprite = @nestSprite
          else
            agent.sprite = @foodSprite
        else
          if withFood
            agent.color = @nestColor
          else
            agent.color = @foodColor

        agent.pheromone = @maxPheromone

      updateAgents: ->
        # gradually leave nest
        for agent in @agents when agent.id < @animator.ticks
          @wiggleUphill agent
          @dropPheromone agent

        null # needed to avoid returning an absurd comprehension

      wiggleUphill: (agent) ->
        if agent.patch.isOnEdge()
          agent.rotate u.degreesToRadians(180)
        else
          nAhead = agent.patch.neighbors(
            heading: agent.heading, cone: u.degreesToRadians(180), radius: 2)
          [patch, max] = nAhead.max ((n) =>
            @targetPheromone(agent, n)), true # => for @

          if max > .001 / @maxPheromone
            agent.face patch.position

        agent.rotate u.randomCentered(@wiggleAngle)
        agent.forward 1

      targetPheromone: (agent, patch) ->
        if agent.carryingFood
          patch.nestPheromone
        else
          patch.foodPheromone

      dropPheromone: (agent) ->
        if (not agent.carryingFood and agent.patch.isFood) or
            (agent.carryingFood and agent.patch.isNest)
          @resetAgent agent, not agent.carryingFood

        if agent.carryingFood
          agent.patch.foodPheromone += 0.1 * agent.pheromone
        else
          agent.patch.nestPheromone += 0.1 * agent.pheromone

        agent.pheromone *= 0.9
      
      updatePatches: ->
        @patches.diffuse "nestPheromone", @diffusionRate
        @patches.diffuse "foodPheromone", @diffusionRate

        for patch in @patches
          patch.foodPheromone *= (1 - @evaporationRate)
          patch.nestPheromone *= (1 - @evaporationRate)

          if patch.isOnEdge()
            patch.foodPheromone = patch.nestPheromone = 0

          unless (patch.isNest or patch.isFood) #  or patch.isOnEdge()
            if patch.foodPheromone > patch.nestPheromone
              patch.color = u.fractionOfColor @foodColor, patch.foodPheromone
            else
              patch.color = u.fractionOfColor @nestColor, patch.nestPheromone

        null

      # utilities
      patchRectangle: (point, dw, dh) ->
        @patches.patchRectangle @patches.patch(point), dw, dh, true

    model = new ABM.AntsModel {
      div: "world",
      patchSize: 6,
      mapSize: 80
    }
    model.start()

    </script>
  </head>
  <body>
    <div id="world"></div>
  </body>
</html>
