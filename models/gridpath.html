<html>
  <head>
    <title>Grid Path Model</title>
    <script src="../lib/agentscript.js"></script>
    <script src="../tools/coffee-script.js"></script>
    <script type="text/coffeescript">

    u = ABM.util # ABM.util alias, u.s is also ABM.shape accessor.
    class MyModel extends ABM.Model
      setup: ->
        @refreshAgents = false # for static agents
        
        # globals
        @prob = 1
        
        # defaults
        @patches.setDefault "labelColor", [255, 255, 255]
        offset = Math.floor @world.size * .45
        # @patches.setDefault "labelOffset", [12,14]
        @patches.setDefault "labelOffset", [offset, offset]
        u.contextTextParams ABM.contexts.patches, "10px sans-serif", "right", "bottom"
        # u.elementTextParams ABM.contexts.patches, "10px sans-serif", "right", "bottom"
        @agents.setDefault "shape", "circle"
        @agents.setDefault "color", ([210, 0, 0])
        @agents.setDefault "size", .4
        @agents.setDefault "hidden", 2.5
        @agents.setDefault "heading",  0 # override promotion to random angle

        @animator.setRate 10

        for patch in @patches.create()
          patch.color = u.randomGray 160, 190
          patch.occupied = false

        @drawGrid [0, 0, 0]
        @walker = @agents.create(1)[0]
        patch0 = @patches[0]
        @walker.moveTo patch0.position
        @walker.penDown = true
        # @walker.color = [210,0,0]
        # @walker.shape = "circle"
        # @walker.size = .4
        @walker.stamp()
        @walker.patch.occupied = true
        # @walker.penDown = true
        # @walker.penSize *= 2.5
        # @setTextParams @patches, "9px sans-serif", "right", "bottom"
        # @setLabelParams @patches, [255, 255, 255], [12, 12]

      step: ->
        @floodFill()
        patches = @patches.asSet (neighbor for neighbor in @walker.patch.neighbors(diamond: 1) when neighbor.ok)
        @drawNum patches.length
        @walker.moveTo patches.sample().position
        @walker.stamp()
        @walker.patch.occupied = true
        (@stop(); console.log "prob: #{@prob}") if @done()
      
      drawNum: (number) ->
        @walker.patch.label = "" + number
        @prob /= number

      done: ->
        @patches.last().occupied
      
      drawGrid: (c) ->
        a = @agents.create(1)[0]
        a.color = c
        a.penSize *= .4
        a.heading = Math.PI / 2

        for x in [0..@patches.max.x]
          a.moveTo x: x, y: 0
          a.penDown = true
          a.forward @patches.max.y
          a.penDown = false
        a.heading = 0

        for y in [0..@patches.max.y]
          a.moveTo x: 0, y: y
          a.penDown = true
          a.forward @patches.max.x
          a.penDown = false
        a.die()
      
      floodFill: ->
        for patch in @patches
          patch.ok = false

        pset = [@patches.last()]

        while pset.length > 0
          p.ok = true for p in pset
          pnext = @asSet u.flatten (p.neighbors(diamond: 1) for p in pset)
          pnext.sort("id").uniq()
          pset = (n for n in pnext when not n.ok and not n.occupied)
        null

    new MyModel({
      div: "layers",
      patchSize: 30,
      min: {x: 0, y: 0},
      max: {x: 10, y: 10}
    })
    .start()
    .debug()

    </script>
  </head>
  <body>
    <div id="layers"></div>
  </body>
</html>
