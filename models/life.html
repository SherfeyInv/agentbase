<html>
  <head>
    <title>Life Model</title>
    <script src="../lib/agentbase.js"></script>
    <script src="../tools/coffee-script.js"></script>
    <script type="text/coffeescript">
    # AgentBase is Free Software, available under GPL v3 or any later version.
    # Original AgentScript code @ 2013, 2014 Owen Densmore and RedfishGroup LLC.
    # AgentBase (c) 2014, Wybo Wiersma.

    # Life provides an implementation of Conway's Game of Life with a twist.
    # This example demonstrates running multiple models on the same page.

    u = ABM.util

    class ABM.LifeModel extends ABM.Model
      startup: ->
        @livingColor = u.color.white
        @deadColor = u.color.black

      setup: ->
        for patch in @patches.create()
          if Math.random() < 0.3
            @birth(patch)
          else
            @death(patch)
        
      step: ->
        # Alternate between updating each patch's living neighbor count,
        # and changing the `living` state variable of each patch.
        # Since both models are triggered by a requestAnimationFrame callback
        # in an ABM.Animator, the models should be guaranteed to run in
        # lock-step.
        if @animator.ticks % 2 is 1
          @countLivingNeighbors()
        else
          @enactBirthsAndDeaths()

      countLivingNeighbors: () ->
        for patch in @patches
          if @linkEnabled
            # If the models are "linked", a patch's neighbors exist
            # only in the model to which it is linked.
            linkedPatch = @linkedModel.patches.patch(patch.position)
          else
            linkedPatch = patch

          patch.liveNeighbors = linkedPatch.neighbors().with((patch) ->
              patch.living).length

      enactBirthsAndDeaths: () ->
        for patch in @patches
          # A patch will go from being dead to being alive
          # if it has exactly three living neighbors
          if patch.liveNeighbors is 3
            @birth(patch)
          else
            # A living patch will stay living only if it
            # has exactly two living neighbors
            unless patch.liveNeighbors is 2
              @death(patch)

      birth: (patch) ->
        patch.living = true
        patch.color = @livingColor

      death: (patch) ->
        patch.living = false
        patch.color = @deadColor

      linkModel: (model) ->
        @linkedModel = model
        @linkEnabled = true

      setupBothModels: () ->
        @setup()
        @linkedModel.setup()

    # Model definition done

    options = {
      patchSize: 8,
      mapSize: 50
    }

    options.div = 'world2'
    model1 = window.model1 = new ABM.LifeModel(options)

    options.div = 'world1'
    model2 = window.model2 = new ABM.LifeModel(options)

    # make the model divs display side by side
    model1.div.style.display = model2.div.style.display = 'inline-block'

    model1.linkModel(model2)
    model2.linkModel(model1)

    model1.start()
    model2.start()

    </script>
  </head>
  <body>
    <div id="world1"></div>
    <div id="world2"></div>
  </body>
</html>
