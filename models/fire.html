<html>
  <head>
    <title>Fire Model</title>
    <script src="../lib/agentbase.js"></script>
    <script src="../tools/coffee-script.js"></script>
    <script type="text/coffeescript">
    # AgentBase is Free Software, available under GPL v3 or any later version.
    # Original AgentScript code @ 2013, 2014 Owen Densmore and RedfishGroup LLC.
    # AgentBase (c) 2014, Wybo Wiersma.

    # Fire is a cellular automata model of fire spreading.

    u = ABM.util

    class ABM.FireModel extends ABM.Model
      setup: ->
        @agentBreeds ["embers", "fires"]
        @agents.setDefault "shape", "square"
        @agents.setDefault "color", [255, 0, 0]
        @agents.setDefault "heading", 0 # override promotion to random angle
        
        @refreshPatches = false
        # No optimizations: 50-55fps
        # @patches.usePixels() # 50-55fps .. not used, refresh off
        # None of the optimizations particularly useful, other than refresh
        # If refresh on, fastPatches is in the 50+fps range.
        
        @density = 60 # percent
        @burnedTrees = 0
        @initialTrees = 0
        
        # defaults
        # @animator.setRate 10, true
        @animator.setRate 60, false
        
        for patch in @patches.create() when u.randomInt(100) < @density
          # u.setColor p.color, 0, 255, 0
          patch.color = [0, 255, 0] # override default, set per patch color

        for patch in @patches
          if patch.position.x is @patches.min.x
            @ignite patch

        trees = []

        for patch in @patches
          if u.colorsEqual patch.color, [0, 255, 0]
            trees.push patch

        @initialTrees = trees.length

        console.log "burnedTrees #{@burnedTrees}"

        @burnedTrees = 0 # reset from initial ignites

      ignite: (patch) ->
        patch.sprout 1, @fires
        # in original model but apparently not needed, refresh off?
        patch.color = [0, 0, 0]
        @burnedTrees++

      fadeEmbers: ->
        for ember in @embers by -1 # -1: allow die() in loop
          ember.color = u.fractionOfColor ember.color, .8
          # or (Math.max a.color...) < 100 , needs parens
          if 100 > Math.max ember.color...
            ember.patch.color = ember.color
            ember.patch.draw @contexts.patches
            ember.die()

      step: ->
        if @animator.ticks % 100 is 0
          console.log @animator.toString()

        unless @agents.any()
          console.log "..stopping, fire done at tick: #{@animator.ticks}"
          @stop()

        for fire in @fires by -1 # -1: allow changeBreed() in loop
          for patch in fire.patch.neighbors(diamond: 1)
            if u.colorsEqual patch.color, [0, 255, 0]
              @ignite patch

          @embers.reBreed fire

        @fadeEmbers()

    model = new ABM.FireModel {
      div: "world",
      patchSize: 2,
      mapSize: 250
    }
    model.start()
    
    </script>
  </head>
  <body>
    <div id="world"></div>
  </body>
</html>
